plugins {
    id 'net.neoforged.gradle.userdev' version "${neogradle_version}"
    id 'com.matthewprenger.cursegradle' version "${cursegradle_version}"
    id 'com.modrinth.minotaur' version "${minotaur_version}"
    id 'com.github.johnrengelman.shadow' version "${shadow_version}"
    id 'mod-update' version "${mod_update_version}"
    id 'net.neoforged.gradle.mixin' version "${neogradle_mixin_version}"
}

apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

version = mod_version
group = mod_packagename
archivesBaseName = mod_archive_name

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version as int)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    client {
        workingDirectory file('run')
        systemProperty 'forge.logging.console.level', 'debug'
        programArguments = ['--username', "henkelmax${new Random().nextInt(1000)}"]
        modSource project.sourceSets.main
    }
    server {
        workingDirectory file('run/server')
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven {
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    mavenLocal()
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    compileOnly "maven.modrinth:jei:${jei_version}"
    compileOnly "maven.modrinth:the-one-probe:${theoneprobe_version}"
    compileOnly "maven.modrinth:jade:${jade_version}"

    shadow "de.maxhenkel.corelib:corelib:${minecraft_version}-${corelib_version}:api"
    runtimeOnly "de.maxhenkel.corelib:corelib:${minecraft_version}-${corelib_version}"
    compileOnly "de.maxhenkel.corelib:corelib:${minecraft_version}-${corelib_version}:javadoc"

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    manifest {
        attributes(['Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_vendor,
                    'Specification-Version'   : '1',
                    'Implementation-Title'    : mod_name,
                    'Implementation-Version'  : mod_version,
                    'Implementation-Vendor'   : mod_vendor,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

processResources {
    filesMatching('**/*.toml') {
        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'mod_vendor': mod_vendor,
                'mod_name': mod_name,
                'neoforge_version_range': neoforge_version_range,
                'loader_version': loader_version,
                'minecraft_version': minecraft_version
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = ''
    relocate 'de.maxhenkel.corelib', "de.maxhenkel.${mod_id}.corelib"
}
jar.finalizedBy('shadowJar')

curseforge {
    apiKey = file('curseforge_api_key.txt').exists() ? file('curseforge_api_key.txt').text : ''
    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = release_type
        addGameVersion minecraft_version
        addGameVersion 'NeoForge'
        addGameVersion "Java ${java_version}"
        mainArtifact(jar) {
            displayName = "[NEOFORGE][${minecraft_version}] ${mod_name} ${mod_version}"
            relations {
                optionalDependency 'jei'
                optionalDependency 'the-one-probe'
                optionalDependency 'jade'
            }
        }
    }
}
tasks.curseforge.dependsOn(shadowJar)

tasks.modrinth.configure({
    group = 'upload'
})
tasks.modrinthSyncBody.configure({
    group = 'upload'
})
modrinth {
    token = file('modrinth_token.txt').exists() ? file('modrinth_token.txt').text : ''
    projectId = modrinth_id
    versionNumber = "neoforge-${mod_version}"
    versionName = "[NEOFORGE][${minecraft_version}] ${mod_name} ${mod_version}"
    uploadFile = jar
    versionType = release_type.toUpperCase()
    changelog = file('changelog.md').text
    gameVersions = [minecraft_version]
    loaders = ['neoforge']
    dependencies {
        optional.project 'jei'
        optional.project 'the-one-probe'
        optional.project 'jade'
    }
}
tasks.modrinth.dependsOn(shadowJar)

modUpdate {
    serverURL = 'https://update.maxhenkel.de/'
    modID = mod_id
    gameVersion = minecraft_version
    modLoader = 'neoforge'
    modVersion = mod_version
    changelogFile = file('changelog.md')
    releaseType = release_type
    tags = recommended == 'true' ? ['recommended'] : []
}